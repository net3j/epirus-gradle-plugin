plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-gradle-plugin'
    id 'java'

    // Required for JFrog Artifactory repository
    id 'jacoco'
    id 'maven-publish'

    // Required for Maven Nexus repository
    id 'maven'
    id 'signing'
    id 'idea'
}

group = 'com.web3labs.epirus'
version = '1.0.0-SNAPSHOT'
description = 'Gradle plugin providing tasks for the Epirus platform.'
sourceCompatibility = JavaVersion.VERSION_1_8

jacoco {
    toolVersion = '0.8.2' // See http://www.eclemma.org/jacoco/.
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

ext {
    web3jVersion = '4.3.0'
    solidityPluginVersion = '0.1.6'
    jerseyVersion = '2.29'
    junitVersion = '4.12'
    wiremockVersion = '2.23.2' 

    ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
    ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
    ossrhRepoUrl = project.hasProperty('ossrhRepoUrl') ? project.property('ossrhRepoUrl') : System.getenv('OSSRH_REPO_URL')
    bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    bintrayGpgPassphrase = project.hasProperty('bintrayGpgPassphrase') ? project.property('bintrayGpgPassphrase') : System.getenv('BINTRAY_GPG_PASSPHRASE')
    isSnapshotVersion = project.version.endsWith("-SNAPSHOT")
    manualRelease = project.hasProperty('manualRelease') ? project.property('manualRelease').toBoolean() : false
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.web3j:web3j-gradle-plugin:$web3jVersion"
    implementation "org.web3j:solidity-gradle-plugin:$solidityPluginVersion"
    implementation "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion"
    
    runtimeOnly "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    runtimeOnly "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    runtimeOnly 'javax.activation:javax.activation-api:1.2.0'
    
    testCompile "junit:junit:$junitVersion"
    testCompile "com.github.tomakehurst:wiremock-jre8-standalone:$wiremockVersion"
}

gradlePlugin {
    plugins {
        epirus {
            id = 'com.web3labs.epirus'
            implementationClass = 'com.web3labs.epirus.gradle.plugin.EpirusPlugin'
        }
    }
}

pluginBundle {
    website = 'https://web3labs.com/'
    vcsUrl = 'https://github.com/web3j/epirus-gradle-plugin'
    description = 'Gradle plugin providing tasks to generate web3j contracts from Solidity.'
    tags = ['ethereum', 'web3.js', 'web3j', 'epirus', 'web3labs']
    plugins {
        epirus {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
        }
    }
}

tasks.withType(Test) {
    reports.html.destination file("${reporting.baseDir}/${name}")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            pom {
                name = project.name
                description = project.description
                url = "https://github.com/web3j/epirus-gradle-plugin"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "conor10"
                        name = "Conor Svensson"
                        email = "conor10@gmail.com"
                    }
                    developer {
                        id = "xaviarias"
                        name = "Xavier Arias"
                        email = "xavi.arias.segui@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/web3j/epirus-gradle-plugin"
                    connection = "scm:https://github.com/web3j/epirus-gradle-plugin.git"
                    developerConnection = "scm:git://github.com/web3j/epirus-gradle-plugin.git"
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            onlyIf {
                // Only run if this is a manual release triggered by a user, or an automated release for snapshots.
                // We don't need to run this for automated releases.
                isSnapshotVersion && ossrhUsername != '' && ossrhPassword != '' && ossrhRepoUrl != ''
            }

            repository(url: ossrhRepoUrl) {
                authentication(
                        userName: ossrhUsername,
                        password: ossrhPassword
                )
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://web3labs.com/'

                scm {
                    url 'https://github.com/web3j/epirus-gradle-plugin'
                    connection 'scm:https://github.com/web3j/epirus-gradle-plugin.git'
                    developerConnection 'scm:git://github.com/web3j/epirus-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'conor10'
                        name 'Conor Svensson'
                        email 'conor10@gmail.com'
                    }
                    developer {
                        id 'xaviarias'
                        name 'Xavier Arias'
                        email 'xavi.arias.segui@gmail.com'
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'web3j-gradle-plugin'
        desc = project.description
        userOrg = 'web3j'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/web3j/web3j-gradle-plugin/issues'
        vcsUrl = 'https://github.com/web3j/web3j-gradle-plugin.git'
        websiteUrl = 'http://web3j.io'
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = bintrayGpgPassphrase
            }
            mavenCentralSync {
                sync = true
                user = ossrhUsername
                password = ossrhPassword
                close = '1'
            }
        }
    }
}

task release {
    dependsOn 'build'
    dependsOn 'uploadArchives'

    doLast {
        // Validates the user inputs before a release can be performed.
        if (isSnapshotVersion) {
            // Only need OSSRH details.
            logger.lifecycle("About to perform {} snapshot release ...", manualRelease ? "a manual" : "an automated")

            if (!ossrhUsername || !ossrhPassword || !ossrhRepoUrl) {
                throw new InvalidUserDataException("Required parameters missing:  'ossrhUsername', 'ossrhPassword', 'ossrhRepoUrl'")
            }

            logger.lifecycle(" - ossrhUsername={}", ossrhUsername)
            logger.lifecycle(" - ossrhPassword={}", ossrhPassword ? "provided" : "not_provided")
            logger.lifecycle(" - ossrhRepoUrl={}", ossrhRepoUrl)
        } else {
            // Release => Need OSSRH and bintray.
            if (!ossrhUsername || !ossrhPassword || !ossrhRepoUrl) {
                throw new InvalidUserDataException("Required parameters missing:  'ossrhUsername', 'ossrhPassword', 'ossrhRepoUrl'")
            }
            if (!bintrayUser || !bintrayKey || !bintrayGpgPassphrase) {
                throw new InvalidUserDataException("Required parameters missing:  'bintrayUser', 'bintrayKey', 'bintrayGpgPassphrase'")
            }
            logger.lifecycle("About to perform {} full release ...", manualRelease ? "a manual" : "an automated")
            logger.lifecycle(" - ossrhUsername={}", ossrhUsername)
            logger.lifecycle(" - ossrhPassword={}", ossrhPassword ? "provided" : "not_provided")
            logger.lifecycle(" - ossrhRepoUrl={}", ossrhRepoUrl)
            logger.lifecycle(" - bintrayUser={}", bintrayUser)
            logger.lifecycle(" - bintrayKey={}", bintrayKey ? "provided" : "not_provided")
            logger.lifecycle(" - bintrayGpgPassphrase={}", bintrayGpgPassphrase ? "provided" : "not_provided")
        }
    }

    // Snapshots go to nexus, non-snapshots go to bintray.
    if (isSnapshotVersion) {
        dependsOn 'uploadArchives'
        tasks.findByName('uploadArchives').mustRunAfter 'build'
    } else {
        dependsOn 'bintrayUpload'
        tasks.findByName('bintrayUpload').mustRunAfter 'build'
    }
}